#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

struct Particle
{
	vec2 pos;
	vec2 vel;
};

// LOOK: These bindings correspond to the DescriptorSetLayouts and
// the DescriptorSets from prepareCompute()!

// Binding 0 : Particle storage buffer (read)
layout(std140, binding = 0) buffer ParticlesA
{
   Particle particlesA[ ];
};

// Binding 1 : Particle storage buffer (write)
layout(std140, binding = 1) buffer ParticlesB
{
   Particle particlesB[ ];
};

layout (local_size_x = 16, local_size_y = 16) in;

// LOOK: rule weights and distances, as well as particle count, based off uniforms.
// The deltaT here has to be updated every frame to account for changes in
// frame rate.
layout (binding = 2) uniform UBO
{
	float deltaT;
	float rule1Distance;
	float rule2Distance;
	float rule3Distance;
	float rule1Scale;
	float rule2Scale;
	float rule3Scale;
	int particleCount;
} ubo;

void main()
{
		// LOOK: This is very similar to a CUDA kernel.
		// Right now, the compute shader only advects the particles with their
		// velocity and handles wrap-around.
		// TODO: implement flocking behavior.

    // Current SSBO index
    uint index = gl_GlobalInvocationID.x;
	  // Don't try to write beyond particle count
    if (index >= ubo.particleCount)
		return;

    vec2 centerOfMass = vec2(0.0, 0.0); // Rule 1
		vec2 seperation = vec2(0.0, 0.0);   // Rule 2
		vec2 alignment = vec2(0.0, 0.0);    // Rule 3
		float neighborOfCountRule1 = 0.0;
		float neighborOfCountRule3 = 0.0;
		float distanceBetweenTwoBoids = 0.0;

    // Read position and velocity
		vec2 vPos = particlesA[index].pos.xy;
    vec2 vVel = particlesA[index].vel.xy;

    for (int i = 0; i < ubo.particleCount; ++i) {
	      if (i == index) {
 	      		continue;
 	      }
        distanceBetweenTwoBoids = distance(particlesA[i].pos, vPos);
				if (distanceBetweenTwoBoids < ubo.rule1Distance) {
     			  centerOfMass += particlesA[i].pos.xy;
 		    	  neighborOfCountRule1 += 1.0;
 		    }

				if (distanceBetweenTwoBoids < ubo.rule2Distance) {
 	  			  seperation -= particlesA[i].pos.xy - vPos;
  			}

        if (distanceBetweenTwoBoids < ubo.rule3Distance) {
				    alignment += particlesA[i].vel.xy;
						neighborOfCountRule3 += 1.0;
				}
		}

		if (neighborOfCountRule1 > 0.0) {
				centerOfMass /= neighborOfCountRule1;
				vVel += (centerOfMass - vPos) * ubo.rule1Scale;
		}

// Refer to project 1 feedback, maybe we do not need to subtract this time?

		if(neighborOfCountRule3 > 0.0) {
				alignment /= neighborOfCountRule3;
				vVel += alignment * ubo.rule3Scale;
		}

		vVel += seperation * ubo.rule2Scale;


		// clamp velocity for a more pleasing simulation.
		vVel = normalize(vVel) * clamp(length(vVel), 0.0, 0.1);

		// kinematic update
		vPos += vVel * ubo.deltaT;

    // Wrap around boundary
		if (vPos.x < -1.0) vPos.x = 1.0;
		if (vPos.x > 1.0) vPos.x = -1.0;
		if (vPos.y < -1.0) vPos.y = 1.0;
		if (vPos.y > 1.0) vPos.y = -1.0;

    particlesB[index].pos.xy = vPos;

    // Write back
    particlesB[index].vel.xy = vVel;
}
